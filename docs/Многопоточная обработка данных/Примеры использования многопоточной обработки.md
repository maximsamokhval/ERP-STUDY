## Примеры

### Общий модуль 

#### ОперативныеВзаиморасчетыСервер

``` bsl

//Очищает переданный регистр расчетов в несколько потоков.
// 
// Параметры:
// 	ИмяРегистра - Строка - Имя очищаемого регистра накопления
// Возвращаемое значение:
// 	Число - Количество обработанных регистраторов
//
Функция ВыполнитьОчисткуРегистраМногопоточно(ИмяРегистра) Экспорт
	
	Потоки = Новый Массив;
	КоличествоСвободныхПотоков = КоличествоПотоковРаспределенияВзаиморасчетов();
	КоличествоОбъектовДляОчистки = РазмерПорцииОбработкиВОдномПотоке() * КоличествоСвободныхПотоков;
	Регистратор = Неопределено;
	КонтрольныйЗапуск = 2;
	КоличествоОбработанныхЗаписей = 0;
	Пока КоличествоОбъектовДляОчистки > 0 И КонтрольныйЗапуск > 0 Цикл
		
		Если КоличествоСвободныхПотоков > 0 Тогда
			
			РазмерВыборки = РазмерПорцииОбработкиВОдномПотоке() * КоличествоСвободныхПотоков;
			ВыборкаДляОчистки = ДанныеДляОчистки(ИмяРегистра, Регистратор, РазмерВыборки);
			КоличествоОбработанныхЗаписей = КоличествоОбработанныхЗаписей + ВыборкаДляОчистки.Количество();
			КоличествоОбъектовДляОчистки = ВыборкаДляОчистки.Количество();
			Если КоличествоОбъектовДляОчистки > 0 Тогда
				
				ДанныеПотока = Новый Массив;
				Пока ВыборкаДляОчистки.Следующий() Цикл
					
					Регистратор = ВыборкаДляОчистки.Регистратор;
					ДанныеПотока.Добавить(Регистратор);
					Если ДанныеПотока.Количество() = РазмерПорцииОбработкиВОдномПотоке() Тогда
						Поток = НовоеОписаниеПотока("ОперативныеВзаиморасчетыСервер.УдалитьЗаписиДокументов");
						Поток.НаименованиеЗадания = НСтр("ru = 'Очистка регистра';
														|en = 'Clearing register'") + " " + ИмяРегистра;
						Поток.ПараметрыПроцедуры.Вставить("ДанныеКОбработке", ДанныеПотока);
						Поток.ПараметрыПроцедуры.Вставить("ИмяРегистра", ИмяРегистра);
						ЗапуститьОбработкуВФоне(Поток);
						Потоки.Добавить(Поток);
						ДанныеПотока = Новый Массив;
					КонецЕсли;
					
				КонецЦикла;
				
				Если ДанныеПотока.Количество() > 0 Тогда
					Поток = НовоеОписаниеПотока("ОперативныеВзаиморасчетыСервер.УдалитьЗаписиДокументов");
					Поток.НаименованиеЗадания = НСтр("ru = 'Очистка регистра';
													|en = 'Clearing register'") + " " + ИмяРегистра;
					Поток.ПараметрыПроцедуры.Вставить("ДанныеКОбработке", ДанныеПотока);
					Поток.ПараметрыПроцедуры.Вставить("ИмяРегистра", ИмяРегистра);
					ЗапуститьОбработкуВФоне(Поток);
					Потоки.Добавить(Поток);
				КонецЕсли;
				
			ИначеЕсли КонтрольныйЗапуск > 0 Тогда
				Регистратор = Неопределено;
				КонтрольныйЗапуск = КонтрольныйЗапуск - 1;
			КонецЕсли;
			
		КонецЕсли;
		
		ОжидатьЗавершениеПотока(Поток);
		
		ЗавершитьПотокиВыполнившиеФЗ(Потоки);
		
		КоличествоДоступныхПотоков = КоличествоПотоковРаспределенияВзаиморасчетов();
		КоличествоСвободныхПотоков = Макс(КоличествоДоступныхПотоков - Потоки.Количество(), 0);
		
	КонецЦикла;
	
	ОжидатьЗавершениеВсехПотоков(Потоки);
	
	Возврат КоличествоОбработанныхЗаписей;
	
КонецФункции


Функция ДанныеДляОчистки(ИмяРегистра, Регистратор, РазмерПорции = 0)
	
	ТекстЗапроса = 
	"ВЫБРАТЬ ПЕРВЫЕ 1000
	|	Расчеты.Регистратор КАК Регистратор
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентамиПоСрокам КАК Расчеты
	|ГДЕ
	|	НЕ Расчеты.Регистратор ССЫЛКА Документ.КорректировкаРегистров
	|	И (Расчеты.Регистратор < &Регистратор ИЛИ &БезОтбораПоРегистратору)
	|СГРУППИРОВАТЬ ПО
	|	Расчеты.Регистратор
	|УПОРЯДОЧИТЬ ПО
	|	Регистратор УБЫВ";
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "РасчетыСКлиентамиПоСрокам", ИмяРегистра);
	Если РазмерПорции > 0 Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "1000", Формат(РазмерПорции, "ЧН=0; ЧГ=0"));
	КонецЕсли;
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Регистратор", Регистратор);
	Запрос.УстановитьПараметр("БезОтбораПоРегистратору", Регистратор = Неопределено);
	Выборка = Запрос.Выполнить().Выбрать();
	Возврат Выборка;
	
КонецФункции


```