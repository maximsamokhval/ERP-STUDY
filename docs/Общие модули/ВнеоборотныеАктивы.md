## Работа с метаданными

Функция ЗапуститьРасчетАмортизацииВФоне запускает фоновое задание 
ВнеоборотныеАктивы.РассчитатьАмортизацию, которое в свою очередь запускает фоновые задания "ВыполнитьОчередьЗаданий"


``` bsl 
// Используется для расчета амортизации ОС в фоне.
//  Допускается расчет по нескольким организациям.
//
// Параметры:
//  ПараметрыРасчета		 - Структура				 - см. НовыеПараметрыРасчетаАмортизации()
//  УникальныйИдентификатор	 - УникальныйИдентификатор	 - Уникальный идентификатор формы.
// 
// Возвращаемое значение:
//  Структура              - параметры выполнения задания: 
//   * АдресХранилища  - Строка     - адрес временного хранилища, в которое будет
//                                    помещен результат работы задания;
//   * АдресХранилищаДополнительный - Строка - адрес дополнительного временного хранилища,
//                                    в которое будет помещен результат работы задания (доступно только если 
//                                    установлен параметр ИспользоватьДополнительноеВременноеХранилище);
//   * ИдентификаторЗадания - УникальныйИдентификатор - уникальный идентификатор запущенного фонового задания;
//   * ЗаданиеВыполнено - Булево - Истина если задание было успешно выполнено за время вызова функции.
// 
Функция ЗапуститьРасчетАмортизацииВФоне(ПараметрыРасчета, УникальныйИдентификатор) Экспорт

	РезультатРасчета = ДлительныеОперации.ЗапуститьВыполнениеВФоне(
						УникальныйИдентификатор,
						"ВнеоборотныеАктивы.РассчитатьАмортизацию",
						ПараметрыРасчета,
						НСтр("ru = 'Расчет амортизации';
							|en = 'Depreciation calculation '"));
	
	Возврат РезультатРасчета;

КонецФункции
```



``` bsl

Функция ВыполнитьОчередьЗаданий(ОчередьЗаданийКРасчету)

	МаксимумЗаданий = ВнеоборотныеАктивыСлужебный.МаксимальноеКоличествоЗаданийДляРасчетаАмортизации();
	ВыполняетсяЗаданий = 0;
	
	ЕстьЗадания = Истина;
	ЕстьОшибки = Ложь;
	
	Пока ЕстьЗадания Цикл
		
		ЕстьЗадания = Ложь;
		
		Для каждого ОписаниеЗадания Из ОчередьЗаданийКРасчету Цикл
			
			Если ОписаниеЗадания.Статус = "" И ВыполняетсяЗаданий < МаксимумЗаданий Тогда
				
				// Запуск задания
				ПараметрыВыполненияВФоне = ДлительныеОперации.ПараметрыВыполненияВФоне(Новый УникальныйИдентификатор);
				ПараметрыВыполненияВФоне.НаименованиеФоновогоЗадания = ОписаниеЗадания.НаименованиеФоновогоЗадания;
				ПараметрыВыполненияВФоне.ЗапуститьНеВФоне = ОписаниеЗадания.ПараметрыЗадания.ВернутьПараметрыРасчета;
				
				РезультатВыполненияЗадания = ДлительныеОперации.ВыполнитьВФоне(
												ОписаниеЗадания.ПроцедураРасчета, 
												ОписаниеЗадания.ПараметрыЗадания, 
												ПараметрыВыполненияВФоне);
												
				ОписаниеЗадания.ОжидатьЗавершение = ПараметрыВыполненияВФоне.ОжидатьЗавершение;
				ОписаниеЗадания.Статус = РезультатВыполненияЗадания.Статус;
				ОписаниеЗадания.ИдентификаторЗадания = РезультатВыполненияЗадания.ИдентификаторЗадания;
				ОписаниеЗадания.АдресРезультата = РезультатВыполненияЗадания.АдресРезультата;
				
				Если ОписаниеЗадания.Статус = "Выполняется" Тогда
					ВыполняетсяЗаданий = ВыполняетсяЗаданий + 1;
				ИначеЕсли ОписаниеЗадания.Статус = "Ошибка" Тогда
					ЕстьОшибки = Истина;
				КонецЕсли;
				
			ИначеЕсли ОписаниеЗадания.Статус = "Выполняется" Тогда
				
				// Проверка выполнения
				
				Задание = ФоновыеЗадания.НайтиПоУникальномуИдентификатору(ОписаниеЗадания.ИдентификаторЗадания);
				Если Задание <> Неопределено И Задание.Состояние = СостояниеФоновогоЗадания.Активно Тогда
					
					Попытка
						Задание.ОжидатьЗавершения(ОписаниеЗадания.ОжидатьЗавершение);
						ОписаниеЗадания.Статус = "Выполнено";
						ВыполняетсяЗаданий = ВыполняетсяЗаданий - 1;
					Исключение
						// Специальная обработка не требуется, возможно исключение вызвано истечением времени ожидания.
					КонецПопытки;
					
				ИначеЕсли Задание = Неопределено 
					ИЛИ Задание.Состояние = СостояниеФоновогоЗадания.ЗавершеноАварийно
					ИЛИ Задание.Состояние = СостояниеФоновогоЗадания.Отменено Тогда
					
					ЕстьОшибки = Истина;
					ОписаниеЗадания.Статус = "Ошибка";
					ВыполняетсяЗаданий = ВыполняетсяЗаданий - 1;
					
				ИначеЕсли Задание.Состояние = СостояниеФоновогоЗадания.Завершено Тогда
					
					ОписаниеЗадания.Статус = "Выполнено";
					ВыполняетсяЗаданий = ВыполняетсяЗаданий - 1;
					
				КонецЕсли; 
				
			КонецЕсли; 
			
			Если ОписаниеЗадания.Статус = "Выполнено" 
				И ЭтоАдресВременногоХранилища(ОписаниеЗадания.АдресРезультата) Тогда
				
				Если НЕ ЕстьОшибки Тогда
					ЕстьОшибки = ПолучитьИзВременногоХранилища(ОписаниеЗадания.АдресРезультата);
				КонецЕсли;
				ОписаниеЗадания.АдресРезультата = Неопределено;
				
			КонецЕсли;
			
			Если ОписаниеЗадания.Статус = "Выполняется" ИЛИ ОписаниеЗадания.Статус = "" Тогда
				ЕстьЗадания = Истина;
			КонецЕсли;
			
		КонецЦикла; 
		
	КонецЦикла; 
	
	Возврат ЕстьОшибки;
	
КонецФункции



```